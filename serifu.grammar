@precedence { line @left, panel @left }

@top Doc { (Page | Spread | newlines)+ }

Page[group="Pages"] {
	PageToken Panel*
}

Spread[group="Pages"] { 
	SpreadToken Panel*
}

Panel[group="Panels"] {
	!panel PanelToken Line*
}


Line[group="Lines"] {
	!line (Text | Note | Sfx) 
}

Sfx {
	Star SfxTranslation SfxSource? newlines
}

Note {
	noteToken newlines
}

Text {
	Source ("/" Style)? ":" Content newlines
}

Source {
	word
}

Style {
	word 
}

Content {
	contentText
}

contentText {
	( UnstyledText | Bold | BoldItal | Ital | Star | Colon | DoubleStar | Underscore )* | BlockText
}

UnstyledText {
	anyText
}

@skip { spaces }

@tokens {
	spaces { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
	newline { $[\r\n\u2028\u2029] }
	newlines { $[\r\n\u2028\u2029]+ }

	PageToken { spaces? "#" ![\n#]* newline}
	SpreadToken { "##" ![\n]* newline}
	@precedence { SpreadToken, PageToken }

	PanelToken { spaces? "-" ![\n]* newline}

	wordChar { std.asciiLetter | $[_$\u{a1}-\u{10ffff}] | std.digit | "-" | "'" | "&" | " "}
	word { (std.asciiLetter | $[\u{a1}-\u{10ffff}] | std.digit) (wordChar* (std.asciiLetter | $[\u{a1}-\u{10ffff}] | std.digit))? }


	anychar { ![\u0009:*_\u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff\r\n\u2028\u2029] }
	anyText { anychar+ }

	Star { "*" }
	Bold {
		Star anychar* Star
	}

	DoubleStar { Star Star }
	BoldItal {
		DoubleStar anychar* DoubleStar
	}

	Underscore { "_" }
	Ital { Underscore anychar* Underscore }

	Colon { ":" }

	noteToken { "!" ![\r\n\u2028\u2029]+ }
	SfxTranslation { ![\n)(]+ }
	SfxSource { "(" ![\n)(]+ ")"? }

	BlockText { "/=" BlockTextRest }
	BlockTextRest { ![=] BlockTextRest | "=" BlockTextAfterEquals}
	BlockTextAfterEquals { "/" | "=" BlockTextAfterEquals | ![/=] BlockTextRest }

	@precedence { anyText, BlockText, SpreadToken, PageToken, PanelToken, SfxSource, SfxTranslation, BoldItal, DoubleStar, Star, Bold, Underscore, Ital, word, spaces, newlines, newline }
}